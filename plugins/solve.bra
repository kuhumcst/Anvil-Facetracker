abc=
  ( solve
  =     vars exps var A Z a b c d
      , solution deps nondeps nondep
    .   !arg:(?vars.?exps)
      & !vars:%?var ?vars
      & out$(var !var)
      & !exps:?A ?a+?b*!var*?c+?d ?Z
      & !A !Z:?exps
      & !b*!c:?deps
      & !a:?nondeps
      &   whl
        ' ( !d:?nondep+?b*!var*?c+?d
          & !nondep+!nondeps:?nondeps
          & !b*!c+!deps:?deps
          )
      & !d+!nondeps:?nondeps
      & ( factorize
        =   a b n c A C T S
          .     !arg:?a+?*?b^-1*?+?c
              & !a+!c:%
              & 0:?S
              &   whl
                ' ( !arg:%?T+?arg
                  &     !S
                      + (   !T:?A*!b^?n*?C
                          & !A*!b^(!n+1)*!C
                        | !T*!b
                        )
                    : ?S
                  )
              & !b^-1*factorize$!S
            | !arg
        )
      &   (!var.factorize$(1+-1*!nondeps+-1)*factorize$!deps^-1)
        : ?solution
      & out$(solution !solution)
      & (   !exps:%
          & sub$(!exps.!solution):?exps
          & solve$(!vars.!exps)
        |     
        )
  )
& "second degree: a:position, b:velocity c:acceleration"
&   solve
  $ ( a b c
    .   period*a+St*b+St2*c+-1*Sh
        St*a+St2*b+St3*c+-1*Sth
        St2*a+St3*b+St4*c+-1*St2h
    )
& "third degree: a:position b:velocity c:acceleration d:change of acceleration"
&   solve
  $ ( a b c d
    .   period*a+St*b+St2*c+St3*d+-1*Sh
        St*a+St2*b+St3*c+St4*d+-1*Sth
        St2*a+St3*b+St4*c+St5*d+-1*St2h
        St3*a+St4*b+St5*c+St6*d+-1*St3h
    );
